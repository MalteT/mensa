
mensa_153           = @{ ^"Cafeteria "? ~ ^"Dittrichring" }
mensa_127           = @{ ^"Cafeteria "? ~ ^"Philipp-Rosenthal-Straße"
                       | ^"physi" ~ (^"c" ~ ^"s"? | ^"k")
                       | ^"chemi" ~ (^"stry" | ^"e") }
mensa_118           = @{ ^"Mensa"? ~ ^"Academica" }
mensa_106           = @{ ^"Mensa am "? ~ ^"Park"
                       | ^"main"
                       | ^"Haupt" ~ (^"c" | ^"k") ~ ^"ampus" }
mensa_115           = @{ ^"Mensa am "? ~ ^"Elsterbecken" }
mensa_162           = @{ ^"Mensaria "? ~ ^"Liebigstraße" }
mensa_111           = @{ ^"Mensa "? ~ ^"Petersteinweg" }
mensa_140           = @{ ^"Mensa "? ~ ^"Schönauer Straße" }
mensa_170           = @{ ^"Mensa "? ~ ^"Tierklinik" }
mensa_all           = @{ ^"all" ~ ^"e"? }
date_yyyymmdd       = @{ ASCII_DIGIT{4} ~ "-" ~ ASCII_DIGIT{2} ~ "-" ~ ASCII_DIGIT{2} }
date_weekday        = @{ ^"Mon" ~ ^"day"?
                       | ^"Tue" ~ ^"sday"?
                       | ^"Wed" ~ ^"nesday"?
                       | ^"Thu" ~ ^"esday"?
                       | ^"Fri" ~ ^"day"?
                       | ^"Sat" ~ ^"urday"? }
exclude_pig         = @{ ^"pig" }
exclude_fish        = @{ ^"fish" }
exclude_alcohol     = @{ ^"alcohol" | ^"booze" }
restriction_vegan   = @{ ^"vegan" }
restriction_vegetarian = @{ ^"vegetarian" | ^"veggie" }
restriction_flexible= @{ ^"flexible" }

mensa_name          = { mensa_153
                      | mensa_127
                      | mensa_118
                      | mensa_106
                      | mensa_115
                      | mensa_162
                      | mensa_111
                      | mensa_140
                      | mensa_170
                      | mensa_all }

date_spec           = { ^"today" | ^"tomorrow" | date_yyyymmdd | date_weekday }

location            = { ^"at" ~ mensa_name }
exclude             = { ^"no" ~ ( exclude_pig | exclude_fish | exclude_alcohol ) }
date                = { ^"on" ~ date_spec }
restriction         = { restriction_vegan | restriction_vegetarian | restriction_flexible }

input               = { SOI ~ ( location | exclude | date | restriction )* ~ EOI }

WHITESPACE          = _{ " " | "\t" }
